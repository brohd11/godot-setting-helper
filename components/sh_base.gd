signal settings_changed

const PRINT_ERR = false

var _settings_dict:Dictionary
var _default_dict:Dictionary
#var _no_default:=false
var _subscribed:Dictionary = {}

var _initialize_queued:bool = false

func _get_settings_object():
	return


func initialize():
	#if _no_default:
		#printerr("Provide default settings in dictionary 'DEFAULTS'.")
		#return
	if _initialize_queued:
		return
	_initialize_queued = true
	
	await Engine.get_main_loop().root.get_tree().process_frame
	var settings = _get_settings_object()
	_on_settings_changed(settings)
	connect_settings_object_signal()
	
	_initialize_queued = false

## Alternative to initialize
func object_initialize(object:Object):
	if not _subscribed.has(object):
		printerr("Object not in setting helper: %s" % [object])
		return
	var subscribed = _subscribed[object].get(Keys.SUBSCRIBED, {})
	_process_dict(object, subscribed, _get_settings_object())
	connect_settings_object_signal()


func connect_settings_object_signal():
	var settings = _get_settings_object()
	if not settings.settings_changed.is_connected(_on_settings_changed):
		settings.settings_changed.connect(_on_settings_changed.bind(settings))

func _subscribe_object(object:Object):
	var script = object.get_script()
	var data = {Keys.NAME: str(object), Keys.SUBSCRIBED:{}}
	if script:
		data[Keys.PATH] = script.resource_path
	_subscribed[object] = data
	

func subscribe_property(object:Object, property_name:StringName, setting_path:StringName, default_value=null):
	if not _default_dict.has(setting_path):
		if default_value == null:
			default_value = object.get(property_name)
			if default_value == null:
				print("Setting '%s' has no default and none provided." % property_name)
				return
		
		_default_dict[setting_path] = default_value
	
	if not _subscribed.has(object):
		_subscribe_object(object)
	var object_data = _subscribed.get(object)
	var subscribes = object_data.get(Keys.SUBSCRIBED)
	subscribes[property_name] = setting_path


func _on_settings_changed(settings):
	for object in _subscribed.keys():
		var data = _subscribed[object]
		if not is_instance_valid(object):
			if PRINT_ERR:
				var nm = data.get(Keys.NAME)
				var path = data.get(Keys.PATH, "")
				var text = "Cannot set setting on freed instance: %s" % nm
				if path != "":
					text += "\nPath: %s" % path
				printerr(text)
			_subscribed.erase(object)
			continue
		var subscribed = data.get(Keys.SUBSCRIBED, {})
		_process_dict(object, subscribed, settings)
	
	settings_changed.emit()




func _process_dict(object:Object, dict:Dictionary, settings_obj):
	for property:StringName in dict.keys():
		var setting_string = dict.get(property)
		if not settings_obj.has_setting(setting_string):
			var default = _default_dict.get(setting_string)
			if default == null:
				default = object.get(property)
				if default == null:
					printerr("Attempted setting '%s' with no default provided." % [setting_string])
					continue
			_set_setting(setting_string, settings_obj, default)
		
		if not property in object:
			printerr("Property not in object: %s -> %s" % [property, object])
		object.set(property, _get_setting(setting_string, settings_obj))

func set_default(setting_string, default_val):
	var settings_obj = _get_settings_object()
	if not settings_obj.has_setting(setting_string):
		settings_obj.set_setting(setting_string, default_val)

func set_setting(setting_string, val):
	_set_setting(setting_string, _get_settings_object(), val)

func _set_setting(setting_string, settings_obj, val):
	settings_obj.set_setting(setting_string, val)
	

func get_setting(setting_string):
	return _get_setting(setting_string, _get_settings_object())

func _get_setting(setting_string, settings_obj):
	return settings_obj.get_setting(setting_string)



class Keys:
	const NAME = &"NAME"
	const PATH = &"PATH"
	const SUBSCRIBED = &"SUBSCRIBED"
